plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.azhapps.listapp"
        minSdk 28
        targetSdk 33
        versionCode 6
        versionName "$version_name"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    signingConfigs {
        def propertiesFile = rootProject.file('local.properties')
        def properties = new Properties()
        properties.load(new FileInputStream(propertiesFile))

        release {
            storeFile file("$rootDir/keystores/keystore.jks")
            storePassword properties.getProperty('keystore.password')
            keyAlias properties.getProperty('keystore.key.alias')
            keyPassword properties.getProperty('keystore.key.password')
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        switch (variant.name) {
            case "debug":
                variant.resValue "string", "app_name", "Lists debug"
                break
            case "release":
                variant.resValue "string", "app_name", "Lists"
                break
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation project(":shared:common")
    implementation project(":shared:navigation")
    implementation project(":shared:network")
    implementation project(':shared:utils')
    implementation project(":feature:lists")
    implementation project(':feature:auth')
    implementation project(':feature:groups')
    implementation project(':feature:more')

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    kapt "dev.enro:enro-processor:$enro_version"
    implementation "dev.enro:enro:$enro_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$app_compat_version"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-core:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.activity:activity-compose:1.5.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
}
